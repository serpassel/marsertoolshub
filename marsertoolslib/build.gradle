apply plugin: 'com.android.library'

def siteUrl = 'https://github.com/serpassel/marsertoolshub'      // Homepage URL of the library
def gitUrl = 'https://github.com/serpassel/marsertoolshub.git'   // Git repository URL
def libDescription = 'Herramientas para android'
def gitTag = '0.0.2'
def pkgName = 'marsertools'
def libGroupId = "es.marser.marserutils"                                          // Maven Group ID for the artifact
def libArtifactId = "marsertoolslib"                                          // Maven Group ID for the artifact
def libName = "marsertoolslib"                                          // Maven Group ID for the artifact
def libVersion = gitTag// This is the library version used when deploying t
def repName = "marsertools-rep"


android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 2
        versionName = libVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.3.1'
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = libGroupId //bintray org/group name
version = libVersion //version


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId libGroupId
                artifactId libArtifactId

                // Add your description here
                name libName
                description libDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'zergio'
                        name 'Sergio Pascual'
                        email 'spseligrat@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

version = libVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//Publicaci√≥n en bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    // Package info for BinTray
    pkg {
        repo = repName
        // it is the name that appears in bintray when logged
        name = pkgName
        desc = libDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
        version {
            name = libVersion
            desc = libDescription
            released = new Date()
            vcsTag = gitTag
        }
    }
}


